############## Only usable on UNIX platforms
if (NOT UNIX)
  message(FATAL_ERROR "_mkl.cmake is only usable on UNIX platforms")
endif(NOT UNIX)
if (DEFINED CMAKE_SYSTEM_NAME)
  set(_cmake_system_name ${CMAKE_SYSTEM_NAME})
else(DEFINED CMAKE_SYSTEM_NAME)
  set(_cmake_system_name ${CMAKE_HOST_SYSTEM_NAME})
endif(DEFINED CMAKE_SYSTEM_NAME)

############## Top tools dir
if (DEFINED ENV{INTEL_DIR})
  set(_intel_dir $ENV{INTEL_DIR})
else(DEFINED ENV{INTEL_DIR})
  set(_intel_dir /opt/intel)
endif()
set(INTEL_DIR "${_intel_dir}" CACHE PATH "Intel tools root directory")

############## MKL
if (DEFINED ENV{MKLROOT})
  set(_mkl_root_dir "$ENV{MKLROOT}")
else(DEFINED ENV{MKLROOT})
  set(_mkl_root_dir "${INTEL_DIR}/mkl")
endif(DEFINED ENV{MKLROOT})
set(MKL_ROOT_DIR "${_mkl_root_dir}" CACHE PATH "Intel MKL root directory")

if (APPLE)
  set(_mkl_lib_dir "${MKL_ROOT_DIR}/lib")
else(APPLE)
  set(_mkl_lib_dir "${MKL_ROOT_DIR}/lib/intel64")
endif(APPLE)

set(MKL_THREADING "SEQ" CACHE STRING "MKL backend: SEQ(default), TBB, or OMP")
if (MKL_THREADING STREQUAL "SEQ")
  set(_mkl_backend_id "sequential")
elseif (MKL_THREADING STREQUAL "TBB")
  include(${CMAKE_CURRENT_LIST_DIR}/_tbb.cmake)
  set(_mkl_backend_id "tbb_thread")
  foreach(_libdir ${TBB_LIBRARY_DIRS})
    list(APPEND _tbb_libdir_dashLs "-L${_libdir}")
    list(APPEND _tbb_libdir_rpaths "-Wl,-rpath,${_libdir}")
  endforeach(_libdir)
else()
  set(_mkl_backend_id "intel_thread")
endif()

if (INTEGER4)
  set(_mkl_fortran_int lp64)
else(INTEGER4)
  set(_mkl_fortran_int ilp64)
endif(INTEGER4)

# initialize BLA_STATIC, if needed
if (BUILD_SHARED_LIBS)
  set(_bla_static FALSE)
else (BUILD_SHARED_LIBS)
  set(_bla_static TRUE)
endif (BUILD_SHARED_LIBS)
set(BLA_STATIC ${_bla_static} CACHE BOOL "Whether to use static linkage for BLAS, LAPACK, and related libraries")

if (BLA_STATIC)
  set(_mkl_libraries "-lmkl_intel_${_mkl_fortran_int}" "-lmkl_${_mkl_backend_id}" "-lmkl_core" "-L${_mkl_lib_dir}")
  if (_cmake_system_name STREQUAL Linux)
    list(PREPEND _mkl_libraries "-Wl,--no-as-needed")
  elseif(_cmake_system_name STREQUAL Darwin)
    list(APPEND _mkl_libraries "-Wl,-rpath,${_mkl_lib_dir}")
  endif()
else(BLA_STATIC)
  set(_mkl_libraries "${_mkl_lib_dir}/libmkl_intel_${_mkl_fortran_int}.a" "${_mkl_lib_dir}/libmkl_${_mkl_backend_id}.a" "${_mkl_lib_dir}/libmkl_core.a")
  if (_cmake_system_name STREQUAL Linux)
    list(PREPEND _mkl_libraries "-Wl,--start-group")
    list(APPEND _mkl_libraries "-Wl,--end-group")
  endif(_cmake_system_name STREQUAL Linux)
endif(BLA_STATIC)
if (MKL_THREADING STREQUAL "TBB")
  list(APPEND _mkl_libraries "-ltbb" "${_tbb_libdir_dashLs}")
  if (BUILD_SHARED_LIBS AND APPLE)
    list(APPEND _mkl_libraries "${_tbb_libdir_rpaths}")
  endif(BUILD_SHARED_LIBS AND APPLE)
elseif (MKL_THREADING STREQUAL "OMP")
  # only using Intel OMP lib
  list(APPEND _mkl_libraries "-liomp5")
endif()
list(APPEND _mkl_libraries "-lpthread;-lm;-ldl")

set(LAPACK_LIBRARIES "${_mkl_libraries}" CACHE STRING "LAPACK libraries")
set(LAPACK_INCLUDE_DIRS ${MKL_ROOT_DIR}/include CACHE STRING "LAPACK include directories")
set(_mkl_compile_definitions "MADNESS_LINALG_USE_LAPACKE;MKL_INT=$<IF:$<BOOL:${INTEGER4}>,int,long>")
set(LAPACK_COMPILE_DEFINITIONS "${_mkl_compile_definitions}" CACHE STRING "LAPACK preprocessor definitions")
